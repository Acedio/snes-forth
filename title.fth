BANK@
LOWRAM BANK!
CREATE TITLE-TICKS 1 CELLS ALLOT
CREATE TITLE-NMI-STATE 1 CELLS ALLOT
CREATE TITLE-STATE 1 CELLS ALLOT
CREATE TITLE-BOBBLE 1 CELLS ALLOT
BANK!

0 CONSTANT TITLE-LOOP
1 CONSTANT TITLE-DONE

: COPY-TITLE-TILES-1
  TITLE-TILES
  TITLE-TILES-BANK
  TITLE-TILES-BYTES 2/
  0x4000
  DMA0-VRAM-LONG-TRANSFER
;

: COPY-TITLE-TILES-2
  TITLE-TILES TITLE-TILES-BYTES 2/ +
  TITLE-TILES-BANK
  TITLE-TILES-BYTES 2/
  0x4000 TITLE-TILES-BYTES 2/ 2/ +
  DMA0-VRAM-LONG-TRANSFER
;

: COPY-TITLE-PALETTE
  TITLE-PAL
  TITLE-PAL-BANK
  TITLE-PAL-BYTES
  0x10
  COPY-CGRAM-PALETTE-LONG
;

: TITLE-COPY-BG1
  TITLE-MAP
  \ Start at the tilemap data area (1kth word).
  0x0400 \ word-indexed
  COPY-BG-TO-VRAM
;

: TITLE-NMI
  TITLE-NMI-STATE @ CASE
    0 OF
      COPY-TITLE-TILES-1

      1 TITLE-NMI-STATE +!
    ENDOF
    1 OF
      COPY-TITLE-TILES-2

      1 TITLE-NMI-STATE +!
    ENDOF
    2 OF
      COPY-TITLE-PALETTE

      1 TITLE-NMI-STATE +!
    ENDOF
    3 OF
      STARS-NMI IF
        1 TITLE-NMI-STATE +!
      THEN
    ENDOF
    4 OF
      TITLE-COPY-BG1

      1 TITLE-NMI-STATE +!
    ENDOF
    5 OF
      \ Layers 1 and turn off OBJ.
      0x01 0x11 BG-LAYER-ENABLE MASK!

      \ Set BG1 base (VRAM @ 0x800 (0x400.w))
      4 0x2107 C!

      \ Zero X shift for BG1
      0x00 0x210D C!
      0x00 0x210D C!
      \ Sin bobble for BG1 Y shift
      TITLE-BOBBLE @
      DUP 0x210E C!
      HIBYTE 0x210E C!

      STARS-NMI DROP
    ENDOF
  ENDCASE

  \ Maximum screen brightness
  0x0F 0x2100 C!
;

: TITLE-INIT
  0 TITLE-NMI-STATE !
  0 TITLE-TICKS !
  TITLE-LOOP TITLE-STATE !

  \ - BG1 4*4K words = 16K.w start
  0x0004 0x000F BG-BASE-ADDRESSES MASK!
  0x11   0x11   BG-MODE           MASK!

  STARS-INIT
;

: 2^12/
  HIBYTE LSR LSR LSR LSR \ Shift right 12 bits
  DUP 0x0008 AND 0<> IF 0xFFF0 OR THEN \ Sign extend.
;

( ticks -- )
: UPDATE-BOBBLE
  0xFF AND
  SIN-LUT \ Output ranges from 0x8001 to 0x7FFF
  2^12/
  10 + TITLE-BOBBLE !
;

\ Returns TRUE when done with title.
: TITLE-MAIN
  1 TITLE-TICKS +!

  STARS-MAIN

  TITLE-STATE @ CASE
    TITLE-LOOP OF
      JOY1-PRESSED @ BUTTON-START AND 0<> IF
        AUDIO-PLAY-SFX
        TITLE-DONE TITLE-STATE !
      THEN
      TITLE-TICKS @ UPDATE-BOBBLE
    ENDOF
    TITLE-DONE OF
      \ Extra state so the sound plays immediately.
      TRUE EXIT
    ENDOF
  ENDCASE

  FALSE
;
