: COPY-STARS
  STARS-TILES
  STARS-TILES-BANK
  STARS-TILES-BYTES
  STARS-BG-TILE-BASE TILE-BASE-TO-VRAM-WORD
  DMA0-VRAM-LONG-TRANSFER
;

: COPY-FARSTARS
  FARSTARS-TILES
  FARSTARS-TILES-BANK
  FARSTARS-TILES-BYTES
  FARSTARS-BG-TILE-BASE TILE-BASE-TO-VRAM-WORD
  DMA0-VRAM-LONG-TRANSFER
;

: COPY-STARS-PALETTE
  STARS-PAL
  FARSTARS-PAL-BANK
  STARS-PAL-BYTES
  \ TODO: This is never actually used, the tilemap defaults to the 0th palette.
  0x20
  COPY-CGRAM-PALETTE-LONG
;

: COPY-FARSTARS-PALETTE
  FARSTARS-PAL
  FARSTARS-PAL-BANK
  FARSTARS-PAL-BYTES
  0x00
  COPY-CGRAM-PALETTE-LONG
;

: STARS-COPY-BG2
  STARFIELD-MAP
  STARS-BG-MAP-BASE MAP-BASE-TO-VRAM-WORD
  COPY-BG-TO-VRAM
;

: STARS-COPY-BG3
  FARSTARS-MAP
  FARSTARS-BG-MAP-BASE MAP-BASE-TO-VRAM-WORD
  COPY-BG-TO-VRAM
;

BANK@
LOWRAM BANK!
CREATE STARS-TICKS 1 CELLS ALLOT
CREATE STARS-NMI-STATE 1 CELLS ALLOT
BANK!

: SCROLL-STARFIELD
  STARS-TICKS @
  LSR LSR LSR
  \ X for BG2
  DUP 0x210F C!
  DUP HIBYTE 0x210F C!
  \ Y for BG2
  LSR LSR
  DUP 0x2110 C!
  DUP HIBYTE 0x2110 C!

  \ X for BG3
  DUP 0x2111 C!
  DUP HIBYTE 0x2111 C!

  \ Y for BG3
  LSR LSR
  DUP 0x2112 C!
  HIBYTE 0x2112 C!
;

\ Returns TRUE when all initialization has completed.
( -- done-loading )
: STARS-NMI
  STARS-NMI-STATE @ CASE
    0 OF
      0x1460 SET-BACKDROP-COLOR

      COPY-STARS
      COPY-STARS-PALETTE

      1 STARS-NMI-STATE +!
      FALSE
    ENDOF
    1 OF
      COPY-FARSTARS
      COPY-FARSTARS-PALETTE

      1 STARS-NMI-STATE +!
      FALSE
    ENDOF
    2 OF
      STARS-COPY-BG2

      1 STARS-NMI-STATE +!
      FALSE
    ENDOF
    3 OF
      STARS-COPY-BG3

      1 STARS-NMI-STATE +!
      FALSE
    ENDOF
    4 OF
      0x06 0x06 BG-LAYER-ENABLE MASK!

      STARS-BG-TILE-BASE BG2-TILE-BASE!
      FARSTARS-BG-TILE-BASE BG3-TILE-BASE!

      STARS-BG-MAP-BASE 0x2108 C!
      FARSTARS-BG-MAP-BASE 0x2109 C!

      SCROLL-STARFIELD
      TRUE
    ENDOF
  ENDCASE
;

: STARS-INIT
  0 STARS-NMI-STATE !
  0 STARS-TICKS !
;

: STARS-MAIN
  1 STARS-TICKS +!
;
