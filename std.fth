: ' WORD FIND DROP ; LABEL _TICK
: POSTPONE ' COMPILE, ; IMMEDIATE

: NIP SWAP DROP ;
: OVER >R DUP R> SWAP ;
: TUCK SWAP OVER ;
: ROT >R SWAP R> SWAP ;
: -ROT ROT ROT ; LABEL _NROT

: ['] LIT [ ' LIT XT, ] COMPILE, ' XT, ; IMMEDIATE LABEL _BRACKET_TICK

: 0= 0 = ; LABEL _0_EQ
: 0<> 0 <> ; LABEL _0_NE
: 0< 0 < ; LABEL _0_GT
: 0> 0 > ; LABEL _0_LT

: IF ['] BRANCH0 COMPILE, HERE 0 , ; IMMEDIATE
: THEN DUP >R HERE SWAP ADDRESS-OFFSET R> ! ; IMMEDIATE
: ELSE ['] BRANCH COMPILE, HERE 0 , SWAP POSTPONE THEN ; IMMEDIATE

: BEGIN HERE ; IMMEDIATE
: UNTIL ['] BRANCH0 COMPILE, HERE ADDRESS-OFFSET , ; IMMEDIATE

: WHILE POSTPONE IF SWAP ; IMMEDIATE
: REPEAT ['] BRANCH COMPILE, HERE ADDRESS-OFFSET , POSTPONE THEN ; IMMEDIATE

: CR S" 
" TYPE ;

: ." POSTPONE S" ['] TYPE COMPILE, ; IMMEDIATE LABEL _TYPE_SLIT

: [CHAR] KEY DROP KEY ['] LIT COMPILE, , ; IMMEDIATE

: ( BEGIN KEY [CHAR] ) = UNTIL ; IMMEDIATE LABEL _L_PAREN

( Do comments work now? )

( Seems like it!
  Woohoo! We can finally comment our Forth code! )

( I feel like the way I'm grabbing a CR char here probably isn't portable. )
: \ BEGIN KEY [CHAR] 
= UNTIL ; IMMEDIATE LABEL _BACKSLASH

: ABORT" POSTPONE IF
         POSTPONE S"
         ['] TYPE COMPILE,
         ['] ABORT COMPILE,
         POSTPONE THEN ; IMMEDIATE LABEL _ABORT_S

( These should actually be defined in Lua so we can always use 1-width
  chars/cells/addrs )
: CHARS ;
: CHAR+ 1 + ;
: CELLS 2* ;
: CELL+ 2 + ;
: ADDRS DUP 2* + ;
: ADDR+ 3 + ;

