: ' WORD FIND DROP ; LABEL _TICK
: POSTPONE ' COMPILE, ; IMMEDIATE

: ['] A.LIT [ ' A.LIT XT, ] COMPILE, ' XT, ; IMMEDIATE LABEL _BRACKET_TICK

: 0= 0 = ; LABEL _0_EQ
: 0<> 0 <> ; LABEL _0_NE
: 0< 0 < ; LABEL _0_GT
: 0> 0 > ; LABEL _0_LT

: IF ['] BRANCH0 COMPILE, HERE 0 , ; IMMEDIATE
: THEN A.DUP A.>R HERE A.SWAP ADDRESS-OFFSET A.R> ! ; IMMEDIATE
: ELSE ['] BRANCH COMPILE, HERE 0 , A.SWAP POSTPONE THEN ; IMMEDIATE

: BEGIN HERE ; IMMEDIATE
: UNTIL ['] BRANCH0 COMPILE, HERE ADDRESS-OFFSET , ; IMMEDIATE

: WHILE POSTPONE IF A.SWAP ; IMMEDIATE
: REPEAT ['] BRANCH COMPILE, HERE ADDRESS-OFFSET , POSTPONE THEN ; IMMEDIATE

: TEST-UNTIL 0 BEGIN 1+ DUP . DUP 5 >= UNTIL ;

: TEST-WHILE 0 BEGIN 1+ DUP 5 <= WHILE DUP . REPEAT ;

: CR ." 
" ;

: [CHAR] KEY DROP KEY ['] LIT COMPILE, , ; IMMEDIATE

: ( BEGIN KEY [CHAR] ) = UNTIL ; IMMEDIATE LABEL _L_PAREN

( Do comments work now? )

( Seems like it!
  Woohoo! We can finally comment our Forth code! )

( I feel like the way I'm grabbing a CR char here probably isn't portable. )
: \ BEGIN KEY [CHAR] 
= UNTIL ; IMMEDIATE LABEL _BACKSLASH

: MAIN TEST-WHILE ." DONE!" CR ;

MAIN
